/**
 * This is the Gradle Build for the IDEA Data Portal survey data submission
 * command line interface project. It will compile, test, check for code quality,
 * and create a runnable artifact. It will use Snyk and CodeNarc to help sure
 * code quality. It has Eclipse and IntelliJ plugin to help support developers that
 * use those IDEs.
 *
 * @author Todd Wallentine twallentine AT anthology com
 */

plugins {
    id "java"
    id "groovy"
    id "application"
    id 'idea'    
    id 'eclipse'
    id 'project-report'
    id 'codenarc'    
    id "io.snyk.gradle.plugin.snykplugin" version "0.4"
}

application {
    applicationName = "idpc-submit-survey-data"
    mainClass = "org.ideaedu.Main"
}

snyk {
   api = project.properties["snyk_token"] ?: System.getenv("SNYK_TOKEN")
   severity = snyk_severity
   autoDownload = true
   autoUpdate = true
   arguments = '--all-sub-projects'
}
check.finalizedBy('snyk-test')

/**
 * CodeNarc provides static code analysis to help ensure that the code quality
 * stays high.
 */
codenarc {
    toolVersion = "3.1.0"

    ignoreFailures = true
    configFile = rootProject.file("config/codenarc/ideaRuleSet.groovy")
}
codenarcMain {
    compilationClasspath = sourceSets.main.compileClasspath + sourceSets.main.output
}

repositories {
    mavenLocal()

    maven {
        url azure_url
        name azure_name
        credentials {
            username project.properties["azure_username"] ?: System.getenv("SYSTEM_USER")
            password project.properties["azure_password"] ?: System.getenv("SYSTEM_ACCESSTOKEN")
        }
        authentication {
            basic(BasicAuthentication)
        }
    }

    mavenCentral() {
        content {
            /* Avoid trying to get IDEA libraries from a public repo */
            excludeGroup "org.ideaedu"
            excludeGroup "idea"
        }        
    }
}

dependencies {
    implementation group: 'org.ideaedu', name: 'rest-models', version: '1.13.+' // TODO: Remove this dependency???
    implementation group: 'org.apache.groovy', name: 'groovy', version: '4.0.4'
    implementation group: 'org.apache.groovy', name: 'groovy-json', version: '4.0.4'
	implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.1.3'
	implementation group: 'commons-cli', name: 'commons-cli', version: '1.5.0'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.1' // TODO: Remove this dependency
	implementation group: 'joda-time', name: 'joda-time', version: '2.11.1' // TODO: Remove this dependency???
}
